*KNNL926.sas, one-way analysis of covariance 
   using data from Table 25.1;
options nocenter;
data crackers; 
	infile 'h:\stat512\datasets\CH25TA01.DAT';
   	input cases last treat store;
proc print data=crackers; 
run;
title1 'Plot of the data';
symbol1 v='1' i=none c=black;
symbol2 v='2' i=none c=black;
symbol3 v='3' i=none c=black;
proc gplot data=crackers; 
   plot cases*last=treat;
run;
title1 'Plot of the data with lines';
symbol1 v='1' i=rl c=black;
symbol2 v='2' i=rl c=black;
symbol3 v='3' i=rl c=black;
proc gplot data=crackers; 
   plot cases*last=treat;
run;
proc glm data=crackers; 
   class treat;
   model cases=last treat/solution clparm;
run;
proc glm data=crackers; 
   class treat;
   model cases=last treat;
   lsmeans treat/stderr tdiff pdiff cl;
run;
title1 'Plot of the data with the model';
proc glm data=crackers; 
   class treat;
   model cases=last treat;
   output out=crackerpred p=pred;
data crackerplot; set crackerpred; 
   drop cases pred;
   if treat eq 1 then do 
     cases1=cases; 
     pred1=pred;
     output; end;
   if treat eq 2 then do 
     cases2=cases; 
     pred2=pred;
     output; end;
  if treat eq 3 then do 
     cases3=cases; 
     pred3=pred;
     output; end;
proc print data=crackerplot; 
run;
symbol1 v='1' i=none c=black;
symbol2 v='2' i=none c=black;
symbol3 v='3' i=none c=black;
symbol4 v=none i=rl c=black;
symbol5 v=none i=rl c=black;
symbol6 v=none i=rl c=black;
proc gplot data=crackerplot; 
   plot (cases1 cases2  cases3 pred1 pred2 pred3)*last/overlay;
run;
title1 'Check for equal slopes';
proc glm data=crackers; 
   class treat;
   model cases=last treat last*treat;
run;
* Try it without covariate;
title1 'No covariate';
proc glm data=crackers; 
   class treat;
   model cases=treat;
   output out=nocov p=pred;
run;
symbol1 v=circle i=none c=black;
symbol2 v=none i=join c=black;
proc gplot data=nocov;
	plot (cases pred)*treat/overlay;
run;
data crackerdiff;
	set crackers;
	casediff = cases - last;
proc glm data=crackerdiff;
	class treat;
	model casediff = treat;
	means treat / tukey;
run;
quit;

