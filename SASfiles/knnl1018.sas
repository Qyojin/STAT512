*KNNL1018.sas, three-way anova using data in Table 23.4;

options nocenter;
data exercise; 
	infile 'h:\stat512\datasets\CH23TA04.DAT';
	input extol gender fat smoke;
data exercise; 
	set exercise;
	gfs = 100*gender + 10*fat + smoke;
proc print data=exercise; 
run;
proc sort data=exercise; 
	by gfs;
run;
title1 'Plot of the data';
symbol1 v=circle i=none c=black;
proc gplot data=exercise; 
	plot extol*gfs/ haxis = 111 112 121 122 211 212 221 222;
run;
proc sort data=exercise; 
	by gender fat smoke;
proc means data=exercise; 
	output out=exer2 mean=avextol;
	by gender fat smoke;
data exer2; 
	set exer2;
	fs = fat*10 + smoke;
proc print data=exer2;
run;
proc sort data=exer2; by fs;
title1 'Plot of the means';
symbol1 v='M' i=join c=black;
symbol2 v='F' i=join c=black;
proc gplot data=exer2; 
	plot avextol*fs=gender / haxis = 11 12 21 22;
run;
proc glm data=exercise; 
	class gender fat smoke;
	model extol=gender|fat|smoke / solution;
	* model gender fat smoke gender*fat gender*smoke fat*smoke gender*fat*smoke;
	means gender*fat*smoke;
run;
data exercise; set exercise;
	fs = 10*fat + smoke;
run;
proc glm data=exercise; 
	class gender fs;
	model extol=gender fs; 
	means gender fs/tukey;
run;
proc glm data=exercise noprint;
	model extol =;
	output out= mudat p = mu;
proc glm data=exercise noprint;
	class gender;
	model extol = gender;
	output out= Adat p = muA;
proc glm data=exercise noprint;
	class fat;
	model extol = fat;
	output out= Bdat p = muB;
proc glm data=exercise noprint;
	class smoke;
	model extol = smoke;
	output out= Cdat p = muC;
proc glm data=exercise noprint;
	class gender fat;
	model extol = gender*fat;
	output out= ABdat p = muAB;
proc glm data=exercise noprint;
	class gender smoke;
	model extol = gender*smoke;
	output out= ACdat p = muAC;
proc glm data=exercise noprint;
	class fat smoke;
	model extol = fat*smoke;
	output out= BCdat p = muBC;
proc glm data=exercise noprint;
	class gender fat smoke;
	model extol = gender*fat*smoke;
	output out= ABCdat p = muABC;

data parmest;
	merge exercise mudat Adat Bdat Cdat ABdat ACdat BCdat ABCdat;
	alpha = muA - mu;
	beta = muB - mu;
	gamma = muC - mu;
	alphabeta = muAB - (mu+alpha+beta);
	alphagamma = muAC - (mu+alpha+gamma);
	betagamma = muBC - (mu+beta+gamma);
	abc = muABC - (mu+alpha+beta+gamma+alphabeta+alphagamma+betagamma);
proc print data=parmest;
	var gender fat smoke mu alpha beta gamma alphabeta alphagamma betagamma abc;
run;
proc print data=BCdat;
run;
title1 'Mean over gender vs smoke';
symbol1 v=L i=join;
symbol2 v=H i=join;
proc gplot data=BCdat;
	plot muBC*smoke=fat;
run;
quit;
