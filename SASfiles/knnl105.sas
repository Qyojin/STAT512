*NKNW100.sas;
options nocenter;
*Generate quadratic relationship;
title1 'Quadratic relationship';
data quad; 
   do x=1 to 30; 
      y=x*x-10*x+30+25*normal(0);
      output;
   end;
proc reg data=quad;
   model y=x;
   output out=diagquad r=resid;
run;
symbol1 v=circle i=rl;
proc gplot data=diagquad;
   plot y*x;
run;
symbol1 v=circle i=sm60;
proc gplot data=diagquad;
   plot y*x;
proc gplot data=diagquad;
   plot resid*x/ vref=0;
run;

* Generate data where the variance depends on x (heteroscedastic);
title1 'Heteroscedastic';
Data het; 
   do x=1 to 100; 
      y=100*x+30+10*x*normal(0);
      output;
   end;
proc reg data=het;
   model y=x;
   output out=diaghet r=resid;
run;
symbol1 v=circle i=sm60;
proc gplot data=diaghet;
   plot y*x;
proc gplot data=diaghet;
   plot resid*x/vref=0;
run;

* Generate data with an outlier at x=50;
title1 'Outlier at x=50';
data outlier50; 
   do x=1 to 100 by 5; 
      y=30+50*x+200*normal(0);
      output;
   end;
   x=50; y=30+50*50 +10000;
   d='out'; output;
proc print data=outlier50;
proc reg data=outlier50;
   model y=x;
   where d ne 'out';
proc reg data=outlier50;
   model y=x;
   output out=diag50 r=resid;
run;
symbol1 v=circle i=rl;
proc gplot data=diag50;
   plot y*x;
proc gplot data=diag50;
   plot resid*x / vref=0;
run;

* Generate data with an outlier at x = 100;
title1 'Outlier at x=100';
data outlier100; 
   do x=1 to 100 by 5; 
      y=30+50*x+200*normal(0);
      output;
   end;
   x=100; y=30+50*100 -10000;
   d='out'; output;
proc reg data=outlier100;
   model y=x;
   where d ne 'out';
run;
proc reg data=outlier100;
   model y=x;
   output out=diag100 r=resid;
run;
symbol1 v=circle i=rl;
proc gplot data=diag100;
   plot y*x;
proc gplot data=diag100;
   plot resid*x/ vref=0;
run;

title1 'Non-normal errors';
data expo; 
   do x=1 to 100; 
      y=100*x+30+10*ranexp(1); output;
   end; 
proc reg data=expo;
   model y=x; output out=diagexpo r=resid;
proc univariate data=diagexpo plot normal; 
   var resid;
   histogram resid / normal kernel(L=2);
   qqplot resid / normal (L=1 mu=est sigma=est); 
run;

quit;